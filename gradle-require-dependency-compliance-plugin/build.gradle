plugins {
    id 'java'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.13.0'
    id 'org.sonarqube' version '2.6.1'
    id 'jacoco'
    id 'pl.droidsonroids.jacoco.testkit' version '1.0.3'
}

// read all scripts from ./gradle.d
file('gradle.d').listFiles().sort().each {
    if (it =~ /.*\.gradle$/) {
        apply from: relativePath(it)
    }
}

group 'de.acetous'
version = "2.1.2"

gradlePlugin {
    plugins {
        dependencyCompliance {
            id = 'de.acetous.dependency-compliance'
            implementationClass = 'de.acetous.dependencycompliance.DependencyCompliancePlugin'
            displayName = 'Gradle Dependency Compliance Plugin'
            description = 'A Gradle plugin to check and track dependencies in your project. Useful for meeting compliance targets, including 3rd-party libraries.'
        }
    }
}

pluginBundle {
    website = 'https://github.com/acetous/gradle-require-dependency-compliance'
    vcsUrl = 'https://github.com/acetous/gradle-require-dependency-compliance.git'
            tags = ['dependencies', 'compliance']
    }

test {
    if (project.hasProperty("gradleVersion")) {
        systemProperties["gradleVersion"] = project.property("gradleVersion")
    }
}

repositories {
    jcenter()
}

dependencies {
    implementation(
            'com.google.code.gson:gson:2.8.5',
    )
    testImplementation(
            'junit:junit:4.12',
            'commons-io:commons-io:2.4',
            'org.assertj:assertj-core:3.10.0',
            'com.google.guava:guava:25.1-jre',
    )
}

wrapper {
    gradleVersion = "6.8.2"
    distributionType = Wrapper.DistributionType.ALL
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

project.tasks["sonarqube"].dependsOn "jacocoTestReport"
